<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Productversion="">
  <POU Name="FB_DataBuffer" Id="771b055f-506c-4a24-9946-68b5f942a2ae" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DataBuffer
VAR_OUTPUT
	nElements: UDINT;
END_VAR
VAR
	_nDataSize: UDINT;
	
	_pWriteBuffer: POINTER TO BYTE;
	_pWriteBufferTime: POINTER TO ARRAY [1..cEntryCount] OF ULINT;
	_pReadBuffer: POINTER TO BYTE;
	_pReadBufferTime: POINTER TO ARRAY [1..cEntryCount] OF ULINT;
	
	_pBuffer1: POINTER TO BYTE;
	_aBufferTime1: ARRAY [1..cEntryCount] OF ULINT;
	_pBuffer2: POINTER TO BYTE;
	_aBufferTime2: ARRAY [1..cEntryCount] OF ULINT;
	
	_nBufferIdx: INT := 1;
	
	_nBufferEntry_Add: UDINT;
	
	_bLock: BOOL;
	
END_VAR
VAR CONSTANT
	cEntryCount: UDINT := 25;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="70c28580-1843-4f35-94b4-72114bdef5d5">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains: BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode: BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
	nDataSize: UDINT;
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bInCopyCode THEN
	_nDataSize:= nDataSize;
	_Init();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="d2771a75-3fd5-4433-9d43-95d03bc90ccd">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode: BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _pBuffer1 <> 0 THEN
	__DELETE(_pBuffer1);
END_IF

IF _pBuffer2 <> 0 THEN
	__DELETE(_pBuffer2);
END_IF

_pWriteBuffer:= 0;
_pWriteBufferTime:= 0;
_pReadBuffer:= 0;
_pReadBufferTime:= 0;

_nBufferIdx:= 0;
_nBufferEntry_Add:= 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="_Init" Id="979e1e77-5674-4ad0-b225-bca6642f5a34">
      <Declaration><![CDATA[METHOD PRIVATE _Init : BOOL
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _pBuffer1 <> 0 THEN
	__DELETE(_pBuffer1);
END_IF

IF _pBuffer2 <> 0 THEN
	__DELETE(_pBuffer2);
END_IF

_pBuffer1 := __NEW(BYTE, (_nDataSize * cEntryCount));
_pBuffer2 := __NEW(BYTE, (_nDataSize * cEntryCount));

_nBufferIdx:= 0;

Call();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Call" Id="7234625d-654e-42f3-b84a-7e6bcdca4f30">
      <Declaration><![CDATA[METHOD Call : BOOL
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[nElements:= 0;

IF TestAndSet(_bLock) THEN

	_nBufferIdx:= _nBufferIdx + 1;
	IF _nBufferIdx >= 3 THEN
		_nBufferIdx:= 1;
	END_IF

	CASE _nBufferIdx OF
		1:
			_pWriteBuffer:= _pBuffer1;
			_pReadBuffer:= _pBuffer2;

			_pWriteBufferTime:= ADR(_aBufferTime1);
			_pReadBufferTime:= ADR(_aBufferTime2);
		2:
			_pWriteBuffer:= _pBuffer2;
			_pReadBuffer:= _pBuffer1;

			_pWriteBufferTime:= ADR(_aBufferTime2);
			_pReadBufferTime:= ADR(_aBufferTime1);
	END_CASE

	nElements:= _nBufferEntry_Add;

	_nBufferEntry_Add:= 0;
	_bLock:= FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddData" Id="79409082-1fd5-4ce7-8f41-07701b48207f">
      <Declaration><![CDATA[METHOD AddData : BOOL
VAR_INPUT
	pDataIn: PVOID;
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TestAndSet(_bLock) THEN

	IF _pWriteBuffer <> 0 AND pDataIn <> 0 THEN

		IF _nBufferEntry_Add >= cEntryCount THEN
			_nBufferEntry_Add:= cEntryCount - 1;
		END_IF

		MEMCPY(_pWriteBuffer + (_nBufferEntry_Add * _nDataSize), pDataIn, _nDataSize);

		_nBufferEntry_Add:= _nBufferEntry_Add + 1;

		IF _pWriteBufferTime <> 0 THEN
			_pWriteBufferTime^[_nBufferEntry_Add]:= F_GetActualDcTime64();
		END_IF

	END_IF

	_bLock:= FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetData" Id="3ad1f01b-c4eb-419c-bcc3-ddb54f45952a">
      <Declaration><![CDATA[METHOD GetData : BOOL
VAR_INPUT
	nElement: UDINT(1..cEntryCount);
	pDataOut: PVOID;
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nElement <= nElements THEN
	IF pDataOut <> 0 AND _pReadBuffer <> 0 THEN
		MEMCPY(pDataOut, _pReadBuffer + ((nElement-1) * _nDataSize), _nDataSize);
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetTimestamp" Id="457fe021-b398-4c2d-ba0f-1d885b705de5">
      <Declaration><![CDATA[METHOD GetTimestamp : ULINT
VAR_INPUT
	nElement: UDINT(1..cEntryCount);
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _pReadBufferTime <> 0 THEN
	GetTimestamp:= _pReadBufferTime^[nElement];
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>